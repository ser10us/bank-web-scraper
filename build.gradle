import com.eriwen.gradle.js.tasks.MinifyJsTask
import de.undercouch.gradle.tasks.download.Download

plugins {
    id "com.eriwen.gradle.js" version "2.14.1"
    id "de.undercouch.download" version "3.2.0"
}

ext.PHANTOM_JS_VERSION = "2.1.1"
ext.PHANTOM_JS_PLATFORM = "windows"
ext.PHANTOM_JS_FILENAME = "phantomjs-" + PHANTOM_JS_VERSION + "-" + PHANTOM_JS_PLATFORM
ext.PHANTOM_JS_BINARIES_DIR = "phantomjs-binaries"
ext.PHANTOM_JS_BINARIES_UNPACKED_DIR = PHANTOM_JS_BINARIES_DIR + "/unpacked"

javascript.source {
    custom {
        js {
            srcDir projectDir
            include "src/main/js/*.js"
        }
    }
}

javascript.source.custom.js.each { File file ->
    tasks.create(name: "dominifyJs_${file.name}", type: MinifyJsTask) {
        source = file
        dest = "${buildDir}/minifiedJs/${file.name}"
        closure {
            compilationLevel = "SIMPLE_OPTIMIZATIONS"
            warningLevel = "QUIET"
        }
    }
}

task cleanBuildDir {
    new File("${buildDir}").deleteDir()
}

task downloadPhantomJs(type: Download) {
    def phantomJsZipFileName = PHANTOM_JS_FILENAME + ".zip"
    src "https://bitbucket.org/ariya/phantomjs/downloads/" + phantomJsZipFileName
    dest PHANTOM_JS_BINARIES_DIR + "/" + phantomJsZipFileName
    overwrite false
}

task unzipPhantomJs(dependsOn: downloadPhantomJs, type: Copy) {
    from zipTree(downloadPhantomJs.dest)
    into PHANTOM_JS_BINARIES_UNPACKED_DIR
}

task copyPhantomJs(type: Copy) {
    from projectDir
    into buildDir
    include "*.exe"
}

task copyDependencies(type: Copy) {
    from "${projectDir}/dep"
    into "${buildDir}/dep"
    include "*.js"
}

task processJs(dependsOn: tasks.matching { Task task -> task.name.startsWith("dominifyJs") })

task zip(type: Zip) {
    from("${buildDir}/minifiedJs") {
        include("*.js")
    }

    from(PHANTOM_JS_BINARIES_UNPACKED_DIR + "/" + PHANTOM_JS_FILENAME + "/bin") {
        include("phantomjs.exe")
    }

    destinationDir buildDir

    archiveName "bank-web-scraper.zip"
}

unzipPhantomJs.dependsOn cleanBuildDir
processJs.dependsOn unzipPhantomJs
copyPhantomJs.dependsOn processJs
copyDependencies.dependsOn copyPhantomJs
zip.dependsOn copyDependencies
